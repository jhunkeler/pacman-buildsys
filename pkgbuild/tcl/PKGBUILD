# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=tcl
pkgver=8.6.8
pkgrel=1
pkgdesc="The Tcl scripting language"
arch=('x86_64')
url="http://tcl.sourceforge.net/"
license=('custom') depends=('zlib')
options=('staticlibs')
source=(http://downloads.sourceforge.net/sourceforge/tcl/tcl${pkgver}-src.tar.gz)
sha1sums=('0d014d97ca38534d4be1dfc6c563a7a3a6aa737f')
_build_platform=unix

prepare() {
  cd tcl${pkgver}
  for p in ${startdir}/*.patch
  do
      patch -Np0 < $p
  done

  # we build the tcl sqlite interface in sqlite-tcl package
  rm -rf pkgs/sqlite3*
}

build() {
  cd tcl${pkgver}/${_build_platform}
  [[ $CARCH == "x86_64" ]] && BIT="--enable-64bit"
  ./configure --prefix=$BUILD_PREFIX \
      --mandir=$BUILD_PREFIX/share/man \
      --enable-shared \
      --enable-threads \
      $BIT

  make
}

check() {
  cd tcl${pkgver}/${_build_platform}
  #make test
}

package() {
  cd tcl${pkgver}/${_build_platform}
  make INSTALL_ROOT="${pkgdir}" install install-private-headers
  find $pkgdir -type f ! -perm -u+w -print | xargs -n1 chmod u+w

  ln -sf tclsh${pkgver%.*} "${pkgdir}${BUILD_PREFIX}/bin/tclsh"
  #ln -sf libtcl${pkgver%.*}.so "${pkgdir}${BUILD_PREFIX}/lib/libtcl.so"
  install -Dm644 ../license.terms "${pkgdir}${BUILD_PREFIX}/share/licenses/${pkgname}/LICENSE"

  # remove buildroot traces
  sed -e "s#${srcdir}/tcl${pkgver}/unix#${BUILD_PREFIX}/lib#" \
      -e "s#${srcdir}/tcl${pkgver}#${BUILD_PREFIX}/include#" \
      -i "${pkgdir}${BUILD_PREFIX}/lib/tclConfig.sh"

  tdbcver=tdbc1.0.6
  sed -e "s#${srcdir}/tcl${pkgver}/unix/pkgs/$tdbcver#${BUILD_PREFIX}/lib/$tdbcver#" \
      -e "s#${srcdir}/tcl${pkgver}/pkgs/$tdbcver/generic#${BUILD_PREFIX}/include#" \
      -e "s#${srcdir}/tcl${pkgver}/pkgs/$tdbcver/library#${BUILD_PREFIX}/lib/tcl${pkgver%.*}#" \
      -e "s#${srcdir}/tcl${pkgver}/pkgs/$tdbcver#${BUILD_PREFIX}/include#" \
      -i "${pkgdir}${BUILD_PREFIX}/lib/$tdbcver/tdbcConfig.sh"

  itclver=itcl4.1.1
  sed -e "s#${srcdir}/tcl${pkgver}/unix/pkgs/$itclver#${BUILD_PREFIX}/lib/$itclver#" \
      -e "s#${srcdir}/tcl${pkgver}/pkgs/$itclver/generic#${BUILD_PREFIX}/include#" \
      -e "s#${srcdir}/tcl${pkgver}/pkgs/$itclver#${BUILD_PREFIX}/include#" \
      -i "${pkgdir}${BUILD_PREFIX}/lib/$itclver/itclConfig.sh"

  fix_rpath
}

